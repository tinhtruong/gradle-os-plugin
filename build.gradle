import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

group = 'me.tinhtruong.gradle.plugin'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    groovy localGroovy()
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

jar {
    manifest {
        attributes 'Implementation-Version': "${project.version}"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name 'gradle-os-plugin'
                packaging 'jar'
                modelVersion '4.0.0'
                description 'gradle-os-plugin is a simple plugin to detect type and bitness of underlying operating system'
                url 'https://bitbucket.org/tinhtruong/gradle-os-plugin'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    connection 'scm:git:ssh://git@bitbucket.org/tinhtruong/gradle-os-plugin.git'
                    developerConnection 'scm:git:ssh://git@bitbucket.org/tinhtruong/gradle-os-plugin.git'
                    url 'https://bitbucket.org/tinhtruong/gradle-os-plugin'
                }
                developers {
                    developer {
                        id 'tinhtruong'
                        name 'Tinh Truong'
                    }
                }
            }
        }
    }
}
